package org.bytearray.micrecorder.encoder {

    import com.marstonstudio.crossUserServer.util.Constants;

    import flash.utils.ByteArray;
    import flash.utils.Endian;

    public class WavEncoder implements IEncoder {

        private var _bytes:ByteArray = new ByteArray();
        private var _buffer:ByteArray = new ByteArray();
        
        /**
         * 
         * @param volume
         * 
         */        
        public function WavEncoder() {}

        /*
         The canonical WAVE format starts with the RIFF header:

         0         4   ChunkID          Contains the letters "RIFF" in ASCII form
                                        (0x52494646 big-endian form).

         4         4   ChunkSize        36 + SubChunk2Size, or more precisely:
                                        4 + (8 + SubChunk1Size) + (8 + SubChunk2Size)
                                        This is the size of the rest of the chunk
                                        following this number.  This is the size of the
                                        entire file in bytes minus 8 bytes for the
                                        two fields not included in this count:
                                        ChunkID and ChunkSize.

         8         4   Format           Contains the letters "WAVE"
                                        (0x57415645 big-endian form).

         The "WAVE" format consists of two subchunks: "fmt " and "data":
         The "fmt " subchunk describes the sound data's format:

         12        4   Subchunk1ID      Contains the letters "fmt "
                                        (0x666d7420 big-endian form).

         16        4   Subchunk1Size    16 for PCM.  This is the size of the
                                        rest of the Subchunk which follows this number.

         20        2   AudioFormat      PCM = 1 (i.e. Linear quantization)
                                        Values other than 1 indicate some
                                        form of compression.

         22        2   NumChannels      Mono = 1, Stereo = 2, etc.

         24        4   SampleRate       8000, 44100, etc.

         28        4   ByteRate         == SampleRate * NumChannels * BitsPerSample/8

         32        2   BlockAlign       == NumChannels * BitsPerSample/8
                                        The number of bytes for one sample including
                                        all channels. I wonder what happens when
                                        this number isn't an integer?

         34        2   BitsPerSample    8 bits = 8, 16 bits = 16, etc.

         The "data" subchunk contains the size of the data and the actual sound:

         36        4   Subchunk2ID      Contains the letters "data"
                                        (0x64617461 big-endian form).

         40        4   Subchunk2Size    == NumSamples * NumChannels * BitsPerSample/8
                                        This is the number of bytes in the data.
                                        You can also think of this as the size
                                        of the read of the subchunk following this
                                        number.

         44        *   Data             The actual sound data.
         */

        public function encode(samples:ByteArray):ByteArray
        {
            var data:ByteArray = create( samples );
            
            _bytes.length = 0;
            _bytes.endian = Endian.LITTLE_ENDIAN;
            
            _bytes.writeUTFBytes( "RIFF" );
            _bytes.writeInt( uint( data.length + 44 ) );
            _bytes.writeUTFBytes( "WAVE" );
            _bytes.writeUTFBytes( "fmt " );
            _bytes.writeInt( uint( 16 ) );
            _bytes.writeShort( uint( 1 ) );
            _bytes.writeShort( Constants.CHANNELS );
            _bytes.writeInt( Constants.SAMPLE_RATE );
            _bytes.writeInt( uint( Constants.SAMPLE_RATE * Constants.CHANNELS * ( Constants.BIT_RATE >> 3 ) ) );
            _bytes.writeShort( uint( Constants.CHANNELS * ( Constants.BIT_RATE >> 3 ) ) );
            _bytes.writeShort( Constants.BIT_RATE );
            _bytes.writeUTFBytes( "data" );
            _bytes.writeInt( data.length );
            _bytes.writeBytes( data );
            _bytes.position = 0;
            
            return _bytes;
        }
                
        private function create( bytes:ByteArray ):ByteArray
        {
            _buffer.endian = Endian.LITTLE_ENDIAN;
            _buffer.length = 0;
            bytes.position = 0;
            
            while( bytes.bytesAvailable ) {
                _buffer.writeShort(bytes.readFloat() * (0x7fff));
            }
            return _buffer;
        }
    }
}